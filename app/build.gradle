apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def getGitBranch = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        def result = ''
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        result += stdout.toString().trim() + '/'
        stdout.reset()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        result += stdout.toString().trim()

        return result
    }
    catch (ignored) {
        return "N/A"
    }
}

def getGitLocalChanges = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'status', '--porcelain'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "N/A"
    }
}

def getCurrentDate = { ->
    def date = new Date()
    def formattedDate = date.format('dd/MM/yyyy HH:mm')
    return formattedDate.toString()
}

android {
    namespace "io.bzzzil.bottles"
    compileSdkVersion 34

    defaultConfig {
        applicationId "io.bzzzil.bottles"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "build_git_branch", getGitBranch()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
        }
        debug {
            resValue "string", "build_git_branch", getGitBranch()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
            pseudoLocalesEnabled true
        }
    }
    sourceSets {
        main {
            res {
                srcDir 'src/main/res_flags'
            }
        }
    }
    lint {
        htmlReport false
        textOutput file('stdout')
        textReport true
        xmlReport false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:design:25.4.0'
    implementation 'com.google.firebase:firebase-firestore:24.10.0'
    androidTestImplementation 'junit:junit:4.13.2'
}
