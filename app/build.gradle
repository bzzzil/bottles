apply plugin: 'com.android.application'

def getGitRsa = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "N/A";
    }
}

def getGitLocalChanges = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'status', '--porcelain'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "N/A";
    }
}

def getCurrentDate = { ->
    def date = new Date()
    def formattedDate = date.format('dd/MM/yyyy HH:mm')
    return formattedDate.toString()
}

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "io.bzzzil.bottles"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "build_git_rsa", getGitRsa()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
        }
        debug {
            resValue "string", "build_git_rsa", getGitRsa()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
            pseudoLocalesEnabled true
        }
    }
    sourceSets {
        main {
            res {
                srcDir 'src/main/res_flags'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    androidTestCompile 'junit:junit:4.12'
    compile 'com.google.android.gms:play-services-appindexing:9.8.0'
}
