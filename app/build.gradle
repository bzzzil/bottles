apply plugin: 'com.android.application'

def getGitRsa = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "N/A";
    }
}

def getGitLocalChanges = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'status', '--porcelain'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "N/A";
    }
}

def getCurrentDate = { ->
    def date = new Date()
    def formattedDate = date.format('dd/MM/yyyy HH:mm')
    return formattedDate.toString()
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "io.bzzzil.bottles"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "build_git_rsa", getGitRsa()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
        }
        debug {
            resValue "string", "build_git_rsa", getGitRsa()
            resValue "string", "build_git_local_changes", getGitLocalChanges()
            resValue "string", "build_timestamp", getCurrentDate()
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
}
